// Задание переменных
n = 6
T_0 = 1.41
K = 1.284
T_1 = 0
T_i = 7.6
T_d = T_i / 4
T_s = T_d / 8

// Задаем передаточную функцию
s = poly(0, 's')

W_2 = 1 / (1 + s * T_0)^n
W_1 = (1 + 1 / (T_i * s) + (T_d * s) / (1 + T_s * s)) * K * W_2

W = W_1 / (1 + W_1)

// Создание линейной системы на основе передаточной функции
Sys = syslin('c', W)
// A, B, C - матрицы состояний и D - матрица передачи линейной системы
[A, B, C, D] = abcd(Sys)
disp('A = ', A)

// Решение уравнения Ляпунова
// A*H + A^T*H = -I
I = eye(A)
H = lyap(A, -I, 'c')
disp('H = ', H)

// Нахождение собcтвенных векторов H 
hs = spec(H)
disp('Спектр матрицы Н', hs)

// Проверка условия устойчивости
if (hs > 0) then
    disp('H положительно определена. Система устойчива асимптотически')
    // показатель устойчивости
    disp('kappa = ', norm(H, 2))
else
    disp('Система неустойчива')
end
