// Задание переменных
n = 6
T_0 = 1.41
K = 0.98
T_1 = 1.5
T_i = 7.55
T_d = T_i / 4
T_s = T_d / 8

// Задание передаточной функции
s = poly(0, 's')

exp_series = 1 - T_1 * s + (T_1 * s)^2 / 2 - (T_1 * s)^3 / 6 + (T_1 * s)^4 / 24

W_2 = exp_series / (1 + T_0 * s)^n
W_1 = (1 + (1 / (T_i * s))) * K * W_2

W = W_1 / (1 + W_1)

// Создание линейной системы на основе передаточной функции
Sys = syslin('c', W)
h = 0.1
Sysd = dscr(Sys, h)

Ad = Sysd.A
disp('Ad = ', Ad)

// Решение уравнения Ляпунова
// A*H + A^T*H = -I
I = eye(Ad)
Hd = lyap(Ad, -I, 'd')
disp('H = ', Hd)

// Нахождение собcтвенных векторов H 
hs = spec(Hd)
disp('Спектр матрицы Нd', hs)

// Проверка условия устойчивости
if (hs > 0) then
    disp('H положительно определена. Система устойчива асимптотически')
    // показатель устойчивости
    disp('kappa = ', norm(Hd, 2))
else
    disp('Система неустойчива')
end
